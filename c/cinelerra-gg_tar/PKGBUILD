# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Maintainer: pingplug < aur at pingplug dot me >
# Contributor: goodguy <lists.cinelerra-gg.org>

pkgname=cinelerra-gg_tar
pkgver=20251231
pkgrel=1
pkgdesc="Professional video editing and compositing environment"
arch=(x86_64)
url="https://www.cinelerra-gg.org"
license=(GPL-2.0-only)
depends=(alsa-lib aom dav1d fftw flac glu gtk2 libavc1394
         libdv libiec61883 libpulse libtheora libusb libva libvdpau
         libvorbis libvpx libwebp libxv numactl openexr opus

         # namcap implicit depends
         perl libogg libxext freetype2 glib2 at-spi2-core libxft libglvnd libxfixes libsndfile harfbuzz zlib glibc
         libxinerama cairo libx11 fontconfig gdk-pixbuf2 imath pango jbigkit xz bzip2 libjpeg-turbo libpng gcc-libs
         libtiff)
makedepends=(git cmake ctags libxml2 nasm perl-xml-libxml
             perl-xml-parser python xorg-mkfontdir
             xorg-mkfontscale yasm)
conflicts=(cin)
source=("https://download.cinelerra-gg.org/download.php?file=src%2Fcin_5.1.${pkgver}-src.tgz")
sha256sums=('f1377313f6f017a03a677ce89f1340dcf17cbbd522e424fd2df18238fe00b145')

prepare() {
  cd "cinelerra-5.1"
  ./autogen.sh
}

build() {
  cd "cinelerra-5.1"
  ./configure \
    --prefix=/usr \
    --disable-static-build \
    --without-cuda \
    --with-exec-name=cinelerra-gg
  make -j1
}

package() {
  cd "cinelerra-5.1"
  make -j1 DESTDIR="${pkgdir}" install
}
