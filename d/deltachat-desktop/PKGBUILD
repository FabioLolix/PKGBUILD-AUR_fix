# Maintainer: Jikstra <jikstra@disroot.org>

pkgname=deltachat-desktop
pkgver=1.26.0
pkgrel=1
pkgdesc="Privacy oriented chat application built on e-mail"
arch=(x86_64)
url="https://delta.chat/en/"
license=(GPL3)
depends=(electron)
makedepends=(npm nodejs git)
source=("git+https://github.com/deltachat/deltachat-desktop.git#tag=v${pkgver}"
        'deltachat-desktop.desktop'
        'deltachat-desktop.sh')
sha256sums=('SKIP'
            'bb12448df6e2385b37fbfb24bfecec55018986d0dd412aa351bea6ef336089da'
            'c980791bd750c627bd1adb5339c8458e4a39e94cfa5ef3d6cd9db17b9bd7783d')

build() {
    cd "${srcdir}/${pkgname}"

    npm install --verbose
    npm run build

    # Delete development dependencies, we don't need them anymore
    npm prune --production 
}

package() {
    cd "${srcdir}/${pkgname}"
    
    install -d "${pkgdir}/usr/lib/deltachat"

    cp -r node_modules  images tsc-dist build html-dist _locales themes "${pkgdir}/usr/lib/deltachat"

    rm -rf "${pkgdir}/usr/lib/deltachat/node_modules/deltachat-node/prebuilds/win32-x64"
    rm -rf "${pkgdir}/usr/lib/deltachat/node_modules/deltachat-node/prebuilds/darwin-x64"
    rm -rf "${pkgdir}/usr/lib/deltachat/build/vcredist"
    find "${pkgdir}/usr/lib/deltachat/node_modules/" -name *.js.map -exec rm {} \;

    cp index.js package.json "${pkgdir}/usr/lib/deltachat"
    
    install -Dm644 "${srcdir}/deltachat-desktop.desktop" "${pkgdir}/usr/share/applications/deltachat.desktop"
    install -Dm755 "${srcdir}/deltachat-desktop.sh" "${pkgdir}/usr/bin/deltachat"
    
    install -Dm644 ./images/deltachat.png "${pkgdir}/usr/share/pixmaps/deltachat.png"
}
 
