
pkgname=duplicati-git
pkgver=2.0.7.2.2.0.7.2_canary_2023.05.25.r0.g8fad3ed5d
pkgrel=1
pkgdesc="FOSS backup client that securely stores encrypted, incremental, compressed backups on cloud storage services and remote file servers"
arch=(x86_64 i686 pentium4 arm armv6h armv7h aarch64)
url="https://www.duplicati.com/"
license=(LGPL)
depends=(mono )
makedepends=(git nuget mono-msbuild gtk-sharp-2)
optdepends=('notify-sharp: for tray notifications'
            gtk-sharp-2)
install=duplicati.install
source=("git+https://github.com/duplicati/duplicati.git"
        duplicati.service
        duplicati.sysusers
        duplicati.tmpfiles.conf
        duplicati-user.service
        )
sha256sums=('SKIP'
            '2ff538f11370fcdebaf15a46212dde4de9ea2910c3e7e9f3adef1b1d1f7e8d28'
            'b9389b399467f3e02aa8e76bb98f6efbca1166fbc4d0bdf939493f8403462959'
            'b6ca3d280feb753ded94bb44eef821a0dac0c0c7ed7f37dea76d445a64386c86'
            'b32bcb8da734767497671b30f77def8b42fc7910eb1698009050c66abaca5e15')

pkgver() {
  cd "duplicati"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "duplicati"
  #remove creating tar/gzip archive which slow build()
  sed -i '/tar -C "$(PACKAGE_DIR)" -cf "duplicati.tar" ./,//d' Installer/Makefile/Makefile
  sed -i '/gzip --best "duplicati.tar"/,//d'                   Installer/Makefile/Makefile
}

build() {
  cd "duplicati"
  ./make.sh
}

package() {
  cd "duplicati"/Installer/Makefile/pkg
  cp -r . "${pkgdir}"/
  install -d "${pkgdir}/var/lib/duplicati"

  install -Dm644 "${srcdir}"/duplicati.service         -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 "${srcdir}"/duplicati-user.service    -t "${pkgdir}"/usr/lib/systemd/user/
  install -Dm644 "${srcdir}"/duplicati.sysusers        -t "${pkgdir}"/usr/lib/sysusers.d/
  install -Dm644 "${srcdir}"/duplicati.tmpfiles.conf   -t "${pkgdir}"/usr/lib/tmpfiles.d/
}
