# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Roberto Hueso < roberto at robertohueso dot org >

pkgname=ensmallen-git
pkgver=3.10.0.r1.g9aebc1d2
pkgrel=1
pkgdesc="Flexible C++ header library for efficient mathematical optimization"
arch=(any)
url="https://ensmallen.org/"
license=(BSD-3-Clause)
makedepends=(git cmake openmp armadillo lapack)
provides=(ensmallen)
conflicts=(ensmallen)
source=("git+https://github.com/mlpack/ensmallen.git")
sha256sums=('SKIP')

pkgver() {
  cd ensmallen
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "ensmallen" -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -D ensmallen/LICENSE.txt  -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
