# Maintainer: FabioLolix <fabio.loli@disroot.org>
# Contributor: Zdeněk Janeček <jan.zdenek@gmail.com>

pkgname=entangle
pkgver=3.0.r29.gd5bdff8
pkgrel=1
pkgdesc="GTK graphical interface for tethered photography with digital cameras powered by libgphoto2"
arch=(x86_64 i686)
url="https://entangle-photo.org/"
license=(GPL3)
depends=(dbus-glib gdk-pixbuf2 gtk3 libgphoto2 libgudev gobject-introspection lcms2
         libpeas libgexiv2 libraw adwaita-icon-theme gstreamer itstool gst-plugins-base-libs)
makedepends=(git intltool meson gtk-doc)
source=("git+https://gitlab.com/entangle/entangle.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  sed -i "s/'libraw_r'/'raw_r'/g" meson.build
  meson -Denable-gtk-doc=true build-dir --prefix=/usr
  ninja -C build-dir all
}

check() {
  cd "${srcdir}/${pkgname%-git}"
  ninja -C build-dir test
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  DESTDIR="$pkgdir" ninja -C build-dir install
}
