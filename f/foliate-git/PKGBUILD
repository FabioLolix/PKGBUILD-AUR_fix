# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: schw0reismus <schw0reismus@protonmail.com>

pkgname=foliate-git
pkgver=3.2.1.r17.g90fc5de
pkgrel=1
pkgdesc="Simple and modern GTK eBook reader"
arch=(any)
url="https://github.com/johnfactotum/foliate"
license=(GPL-3.0-or-later)
depends=(gjs gtk4 libadwaita webkitgtk-6.0)
makedepends=(meson ninja git)
optdepends=('hyphen: Auto-hyphenation support'
			'hyphen-en: Hyphenation rules for English; you may choose package for your language'
			'dictd: Offline dictionary support'
			'festival: Text-to-speech support'
			'espeak-ng: Text-to-speech support'
            'tinysparql: track file location'
            'speech-dispatcher'
		   )
provides=(foliate)
conflicts=(foliate)
source=("git+https://github.com/johnfactotum/foliate.git#branch=gtk4"
        "git+https://github.com/johnfactotum/foliate-js.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd foliate
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd foliate
  git submodule init
  git config submodule.src/foliate-js.url "${srcdir}/foliate-js"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd foliate
  meson build --prefix=/usr
  ninja -C build
}

package(){
  cd foliate
  DESTDIR="$pkgdir" ninja -C build install
}
