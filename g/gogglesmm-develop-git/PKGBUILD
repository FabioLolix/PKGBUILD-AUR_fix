# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org>
# Contributor: Sander Jansen <sander@knology.net>

pkgname=gogglesmm-develop-git
pkgver=1.2.3.r2.gd6231892
pkgrel=2
pkgdesc="Music Manager and Player"
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="https://gogglesmm.github.io"
license=(GPL3)
depends=(libepoxy libwebp libxcursor libxrandr libxft taglib glu opus libmad faad2 sqlite)
makedepends=(git cmake libpulse alsa-lib)
optdepends=('libpulse: PulseAudio Output'
            'alsa-lib: Alsa Output')
provides=(gogglesmm)
conflicts=(gogglesmm)
source=("git+https://github.com/gogglesmm/gogglesmm.git#branch=develop")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname/-develop-git/}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname/-develop-git/}"
  cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname/-develop-git/}"
  make DESTDIR="$pkgdir" install
}
