# Maintainer: Sander Jansen <sander@knology.net>

pkgname=gogglesmm-git
pkgver=1.2.3.r0.gd3d72690
pkgrel=1
pkgdesc="Music Manager and Player"
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="https://gogglesmm.github.io"
license=(GPL3)
depends=(libepoxy libwebp libxcursor libxrandr libxft taglib glu opus libmad faad2 sqlite)
makedepends=(git cmake libpulse alsa-lib)
optdepends=('libpulse: PulseAudio Output'
            'alsa-lib: Alsa Output')
provides=(gogglesmm)
conflicts=(gogglesmm)
source=("git+https://github.com/gogglesmm/gogglesmm.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname/-git/}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname/-git/}"
  cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname/-git/}"
  make DESTDIR="$pkgdir" install
}
