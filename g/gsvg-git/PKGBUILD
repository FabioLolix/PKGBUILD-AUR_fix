# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gsvg-git
pkgver=0.10.4.r1.g710a15c
pkgrel=1
pkgdesc="GObject API for SVG"
arch=(x86_64)
url="https://gitlab.com/gsvg/gsvg"
license=(LGPL-2.1-only)
depends=(gxml graphene)
makedepends=(git meson vala gobject-introspection)
provides=(gsvg)
conflicts=(gsvg)
source=("git+https://gitlab.com/gsvg/gsvg.git")
sha256sums=('SKIP')

pkgver() {
  git -C gsvg describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson gsvg build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
