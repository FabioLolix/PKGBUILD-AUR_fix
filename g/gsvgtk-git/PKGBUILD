# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gsvgtk-git
pkgver=0.14.1.r7.gfad592c
pkgrel=2
pkgdesc="Library providing Gtk+ widgets to view and modify SVG documents"
arch=(x86_64)
url="https://gitlab.com/gsvg/gsvgtk"
license=(LGPL-2.1-only)
depends=(gsvg-git gtk4)
makedepends=(git meson vala gobject-introspection gtktester)
checkdepends=(xorg-server-xvfb)
provides=(gsvgtk)
conflicts=(gsvgtk)
options=(!lto)
source=("git+https://gitlab.com/gsvg/gsvgtk.git")
sha256sums=('SKIP')

pkgver() {
  git -C gsvgtk describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export CFLAGS+=" -Wno-implicit-function-declaration"
  export CXXFLAGS+=" -Wno-implicit-function-declaration"

  arch-meson gsvgtk build
  ninja -C build
}

check() {
  # check fail if appstream-glib is installed
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
