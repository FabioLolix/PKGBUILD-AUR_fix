# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=gtkam-git
pkgver=1.1.r3.g3526c71
pkgrel=1
pkgdesc="A GTK2 frontend for libgphoto2"
arch=(x86_64 i686)
url="https://github.com/gphoto/gtkam"
license=(GPL-2.0-or-later)
depends=(glibc glib2 gtk2 libgphoto2 libexif libexif-gtk gdk-pixbuf2)
makedepends=(git intltool)
provides=(gtkam)
conflicts=(gtkam)
source=("git+https://github.com/gphoto/gtkam.git")
sha256sums=('SKIP')

pkgver() {
  cd gtkam
  git describe --long --tags --abbrev=7 --exclude gtkam-* | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'

  # 1.1.r3 looks nicer than gtkam.1_1.release.r3 https://github.com/gphoto/gtkam/tags
  #git describe --long --tags --abbrev=7 | sed 's/^gtkam-//;s/\([^-]*-g\)/r\1/;s/-/./g;s/_/./g'
}

prepare() {
  cd gtkam
  autoreconf -f -i
}

build() {
  export CFLAGS+=" -Wno-incompatible-pointer-types"

  cd gtkam
  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--without-bonobo \
	--without-gnome \
	--without-gimp \
	--disable-scrollkeeper

  make
}

package() {
  cd gtkam
  make DESTDIR="${pkgdir}" install
}
