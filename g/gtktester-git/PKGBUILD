# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=gtktester-git
pkgver=2.0.1.r0.gb801197
pkgrel=1
pkgdesc="Library with widgets for Gtk+ widget tests"
arch=(x86_64)
url="https://gitlab.com/gsvg/gtktester"
license=(GPL-3.0-only)
depends=(gtk4 glibc glib2 gxml gresg)
makedepends=(git meson vala gobject-introspection gobject-introspection-runtime)
checkdepends=(xorg-server-xvfb gvfs)
provides=(gtktester)
conflicts=(gtktester)
source=("git+https://gitlab.com/gsvg/gtktester.git")
sha256sums=('SKIP')

pkgver() {
  cd gtktester
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson gtktester build
  ninja -C build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local'  meson test -C build --print-errorlogs | true
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
