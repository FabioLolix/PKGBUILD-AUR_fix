# Maintainer: Sven Fischer <aur.archlinux@linux4tw.de>

pkgname=guayadeque-git
pkgver=0.4.7.r3.g1ae725ae
pkgrel=1
pkgdesc="Lightweight music player"
arch=(i686 x86_64)
url="https://guayadeque.org/"
license=(GPL3)
depends=(wxsqlite3 wxwidgets-gtk3 taglib gst-plugins-base gst-plugins-good jsoncpp)
makedepends=(cmake git)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
provides=(guayadeque)
provides=(guayadeque)
source=("git+https://github.com/anonbeat/guayadeque.git"
        "https://github.com/sluedecke/guayadeque/commit/a9ecb9d8bf785281ed8ccf292cccf3525ffeb457.patch"
)
sha512sums=('SKIP'
            '05cfaca64fa27e901c7b874c0878bd8726fcbcd2d5a38edd67ddfe836266853aeaaf6ed26855e627d5327141e8667403acd56fadd2f6268a7472e4f21421072a')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${pkgname%-git}"
  patch -Np1 -i ../a9ecb9d8bf785281ed8ccf292cccf3525ffeb457.patch
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  ./buildt
  cmake . \
    -DCMAKE_CXX_STANDARD=11 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config

  make
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  make DESTDIR="${pkgdir}" install
}
