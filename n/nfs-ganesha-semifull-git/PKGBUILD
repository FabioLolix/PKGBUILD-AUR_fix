# Maintainer: Wes Jackson <icebal dot 7 at gmail dot com>

pkgname=nfs-ganesha-semifull-git
pkgver=6.dev.2.r0.g83712d6dd
pkgrel=1
pkgdesc="NFS-Ganesha supports both the NFS and 9P protocols in user mode. All options in official Arch repo"
arch=(x86_64 i686 armv7h aarch64)
url="http://nfs-ganesha.github.io/"
license=(GPL3)
depends=(glibc nfsidmap libcap krb5 util-linux-libs e2fsprogs xfsprogs dbus libwbclient jemalloc liburcu python glusterfs acl)
makedepends=(cmake git lsb-release doxygen python-sphinx graphviz) #gssproxy libmspack

# ceph-libs #AUR
# lizardfs #AUR https://github.com/lizardfs/lizardfs last touched 2023/03
# KVSNS ? https://github.com/phdeniel/kvsns last touched 2021/10
# Lustre ? https://github.com/nfs-ganesha/nfs-ganesha/wiki/XFSLUSTRE

provides=(nfs-ganesha)
conflicts=(nfs-ganesha)
source=("git+https://github.com/nfs-ganesha/nfs-ganesha.git"
        "git+https://github.com/nfs-ganesha/ntirpc.git"
        "nfs-ganesha-libntirpc-assert.h-fix.patch::https://github.com/nfs-ganesha/ntirpc/pull/279/commits/1f9bb775d02b8b894f12d8408e35275e329b2da6.patch")
sha256sums=('SKIP'
            'SKIP'
            '309bd1726a8e9545896d8bc766311fa87e6348d369b1a9fb75fd014e344166e5')
backup=(etc/ganesha/ganesha.conf etc/sysconfig/ganesha)

pkgver() {
  cd "nfs-ganesha"
  git describe --long --tags | sed 's/^V//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "nfs-ganesha"
  git submodule init
  git config submodule.src/libntirpc.url "${srcdir}/ntirpc"
  git -c protocol.file.allow=always  submodule update

  cd src/libntirpc
  patch -Np1 -i ${srcdir}/nfs-ganesha-libntirpc-assert.h-fix.patch
}

build() {
  cd "nfs-ganesha/src"
  cmake .  -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_MAN_PAGE=OFF \
    -DUSE_9P=ON \
    -DUSE_RADOS_RECOV=OFF \
    -DRADOS_URLS=OFF \
    -DUSE_FSAL_LUSTRE=OFF \
    -DUSE_FSAL_LIZARDFS=OFF \
    -DUSE_FSAL_CEPH=OFF \
    -DUSE_FSAL_RGW=OFF

  make
}

package() {
  cd "nfs-ganesha/src"
  make install DESTDIR="$pkgdir"
}
