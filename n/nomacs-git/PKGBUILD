# Maintainer: Peter Mattern <pmattern at arcor dot de>

pkgname=nomacs-git
pkgver=3.22.0.rc.1.r53.g52786878
pkgrel=1
pkgdesc='Free, open source image viewer, which supports multiple platforms. Qt6 Lolix pkgbuild'
arch=('i686' 'x86_64')
url='https://github.com/nomacs/nomacs'
license=(GPL-3.0-only)
depends=(exiv2 gcc-libs glibc libraw libtiff opencv qt6-base qt6-svg quazip-qt6)
makedepends=(cmake git git-lfs qt6-tools python)
optdepends=('qt6-imageformats: support additional image formats'
            'kimageformats5: support QOI (Quite OK Image Format)')
provides=(nomacs)
conflicts=(nomacs)
source=("git+https://github.com/nomacs/nomacs.git"
        #"git+https://github.com/nomacs/nomacs-plugins.git"
        )
sha256sums=('SKIP')

export GIT_LFS_SKIP_SMUDGE=1

pkgver() {
  cd nomacs
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd nomacs
  # https://github.com/nomacs/nomacs/issues/951
  #patch -Np1 -i ../nomacs-fix-exiv2-0.28.patch.txt
  # copy plugin sources into place
  #cp -av "${srcdir}/nomacs-plugins/"* "ImageLounge/plugins"
}

build() {
  cd nomacs
  cmake -B build -S ImageLounge -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=6 \
    -DUSE_SYSTEM_QUAZIP=ON \
    -DENABLE_AVIF=ON \
    -DENABLE_TRANSLATIONS=ON

  cmake --build build
}

package() {
  cd nomacs
  DESTDIR="$pkgdir" cmake --install build
}
