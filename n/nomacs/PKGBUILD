# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_ver=3.16
pkgname=nomacs
pkgver=3.22.0
pkgrel=1
epoch=1
pkgdesc="A Qt image viewer"
arch=(x86_64)
url="https://github.com/nomacs/nomacs"
license=(GPL-3.0-only)
depends=(exiv2 hicolor-icon-theme gcc-libs glibc libglvnd libraw libtiff opencv qt6-base qt6-svg quazip-qt6)
makedepends=(cmake qt6-tools python)
optdepends=('qt6-imageformats: support additional image formats'
            'kimageformats: support QOI (Quite OK Image Format)')
source=("$url/archive/refs/tags/${pkgver}.tar.gz")
b2sums=('b99c098a5b0a2f3da53bc19f9dbddbebbca983bdd7e4b83bdcb4e141fed0cf74a8af11341a202a81c8964a3577901a61795214ae5f36ab4d83e21b73bb2dece6')

prepare() {
  cd "${srcdir}/nomacs-${pkgver}"
}

build() {
  cd "${srcdir}"
  cmake -B build -S nomacs-${pkgver}/ImageLounge -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=6 \
    -DUSE_SYSTEM_QUAZIP=ON \
    -DENABLE_AVIF=ON \
    -DENABLE_QUAZIP=ON \
    -DENABLE_TRANSLATIONS=ON

  cmake --build build
}

package() {
  cd "${srcdir}"
  DESTDIR="$pkgdir" cmake --install build
}
