# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=noson-git
pkgver=2.12.9.r0.g741183c
pkgrel=2
pkgdesc="C++ library for accessing SONOS devices"
arch=(x86_64 aarch64 armv7h)
url="https://github.com/janbar/noson"
license=(GPL-3.0-only)
makedepends=(cmake git)
provides=(noson)
conflicts=(noson)
options=(!lto)
source=("git+https://github.com/janbar/noson.git")
sha256sums=('SKIP')

pkgver() {
  cd noson
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  local _flags=(
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  )

  cmake -B build -S "noson" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
