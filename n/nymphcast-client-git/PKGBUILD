# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=nymphcast-client-git
pkgver=0.2.alpha0.r30.gcbff1ee2
pkgrel=1
pkgdesc="Audio and video casting system with support for custom applications."
arch=(x86_64 i686 armv7h aarch64)
url="https://github.com/MayaPosch/NymphCast"
license=(BSD)
depends=(nymphrpc libnymphcast)
makedepends=(git)
source=("git+https://github.com/MayaPosch/NymphCast.git")
sha256sums=('SKIP')

pkgver() {
  cd NymphCast
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd NymphCast/src/client
  make
}

package() {
  cd NymphCast
  install -Dm755 src/client/bin/nymphcast_client -t "${pkgdir}/usr/bin"
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
