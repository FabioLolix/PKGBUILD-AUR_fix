# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributors: LaPingvino

pkgname=openloco-git
pkgver=25.11.r108.gad4f7c42a
pkgrel=1
pkgdesc="An open source re-implementation of Chris Sawyer's Locomotion"
arch=(x86_64)
url="https://github.com/OpenLoco/OpenLoco"
license=(MIT)
depends=(sdl2 libpng openal)

makedepends=(cmake yaml-cpp git gtest fmt)

provides=(openloco)
conflicts=(openloco)
options=(!lto)
source=("git+https://github.com/OpenLoco/OpenLoco.git"
        "openloco.desktop")
sha256sums=('SKIP'
            '57512f00144c1e0d2cc91c3adbf38460d5ec1223afc27bd16e1271760bce02ae')

pkgver() {
  cd OpenLoco
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {

    # -Dfmt_DIR:PATH=/usr/lib/cmake/fmt -> pointless; want lib32-fmt
    # -Dsfl_DIR:PATH=/usr/include/sfl   -> pointless; want to checkout it, no system version


	local _flags=(
    -DFETCHCONTENT_QUIET:BOOL=OFF \
    -DCMAKE_SYSTEM_PROCESSOR=x86_64 \
    -DOPENLOCO_BUILD_TESTS=OFF \
    -DCMAKE_LIBRARY_ARCHITECTURE=x86_64 \
    -DCMAKE_CXX_FLAGS="-Wno-error" \
	)

  cd "${srcdir}/OpenLoco"
  rm -rf build

  cmake -G "Unix Makefiles" -B build -S . -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_DATAROOTDIR=/usr/share \
    -DCMAKE_INSTALL_DATADIR=/usr/share/data \
    -DUSE_SYSTEM_FMT=ON \
    -DCMAKE_SIZEOF_VOID_P=8 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    "${_flags[@]}"

  cmake --build build
}

check() {
  cd "${srcdir}/OpenLoco"
  ctest --test-dir build --output-on-failure
}

package() {
  install -Dm644 "openloco.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "${srcdir}/OpenLoco/src/Resources/src/logo/icon_x16.png" "$pkgdir/usr/share/icons/hicolor/16x16/apps/openloco.png"
  install -Dm644 "${srcdir}/OpenLoco/src/Resources/src/logo/icon_x32.png" "$pkgdir/usr/share/icons/hicolor/32x32/apps/openloco.png"
  install -Dm644 "${srcdir}/OpenLoco/src/Resources/src/logo/icon_x64.png" "$pkgdir/usr/share/icons/hicolor/64x64/apps/openloco.png"
  install -Dm644 "${srcdir}/OpenLoco/src/Resources/src/logo/icon_x128.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/openloco.png"
  install -Dm644 "${srcdir}/OpenLoco/src/Resources/src/logo/icon_x256.png" "$pkgdir/usr/share/icons/hicolor/256x256/apps/openloco.png"
  install -Dm644 "${srcdir}/OpenLoco/src/Resources/src/logo/icon_x512.png" "$pkgdir/usr/share/icons/hicolor/512x512/apps/openloco.png"
  install -Dm644 "${srcdir}/OpenLoco/src/Resources/src/logo/icon_steam.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/openloco.svg"

  cd "${srcdir}/OpenLoco"
  DESTDIR="${pkgdir}" cmake --install build
  install -D "${srcdir}/OpenLoco/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"



  # Remove bundled fmt files to avoid conflicts with system fmt package
  rm -rf "${pkgdir}/usr/lib/libfmt.a"
  rm -rf "${pkgdir}/usr/include/fmt"
  rm -rf "${pkgdir}/usr/lib/cmake/fmt"
  rm -rf "${pkgdir}/usr/lib/pkgconfig/fmt.pc"

  #remove bundled sfl from package
  rm -rf "${pkgdir}/usr/share/include/sfl"
}
