# Maintainer: Campbell Barton <ideasman42@gmail.com>

export GIT_LFS_SKIP_SMUDGE=1

pkgname=opentoonz-git
pkgver=1.7.1.1.r82.10d8aa3de
pkgrel=1
pkgdesc='Software for producing a 2D animation'
arch=(x86_64)
url='https://opentoonz.github.io/e/'
license=(BSD)
depends=(cblas ffmpeg freeglut glew hicolor-icon-theme libmypaint qt5-multimedia qt5-script qt5-svg qt5-serialport superlu opencv)
makedepends=(git boost cmake qt5-tools)
provides=(opentoonz)
conflicts=(opentoonz)
source=("git+https://github.com/opentoonz/opentoonz.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
#  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
#  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"

  TAG=$(git tag --list | tail -n 1 | sed 's/^v//')
  printf "${TAG}.r%s.%s" "$(git rev-list --count v${TAG}..HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  [[ -d build ]] || mkdir build

  cd "${pkgname%-git}"
  # Specify path for ffmpeg
  sed -i 's|"ffmpegPath", QMetaType::QString, ""|"ffmpegPath", QMetaType::QString, "/usr/bin"|' toonz/sources/toonzlib/preferences.cpp
}

build() {
  pushd  "${pkgname%-git}/thirdparty/tiff-4.0.3"
  ./configure --with-pic --disable-jbig
  make
  popd

  cd build
  cmake -G "Unix Makefiles" ../"${pkgname%-git}"/toonz/sources \
    -DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_SKIP_RPATH=YES \
    -DWITH_TRANSLATION=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../"${pkgname%-git}"/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
