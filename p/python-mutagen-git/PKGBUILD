# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=python-mutagen-git
pkgver=1.47.0.r33.g187b984
pkgrel=2
arch=(any)
pkgdesc="An audio metadata tag reader and writer (python library)"
url="https://mutagen.readthedocs.io/en/latest/"
license=(GPL-2.0-or-later)
depends=(python)
makedepends=(git python-build python-wheel python-installer python-setuptools)
checkdepends=(python-coverage python-pytest python-hypothesis python-eyed3 liboggz vorbis-tools faad2) #libogg libvorbis
provides=(python-mutagen)
conflicts=(python-mutagen)
source=("git+https://github.com/quodlibet/mutagen.git#branch=main")
sha256sums=('SKIP')

pkgver() {
  cd "mutagen"
  git describe --long --tags | sed 's/^release.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "mutagen"
  python -m build --wheel --no-isolation
}

check() {
  cd "mutagen"
  python -m coverage run -m unittest discover -v tests
  python -m coverage report -m
  #pytest -v
}

package() {
  cd "mutagen"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {NEWS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
