# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Philipp KÃ¼hn <p dot kuehn at posteo dot de>
# Contributor: hrdl <git@hrdl.eu>

pkgname=sioyek-git
pkgver=2.0.0.r103.g5517425
pkgrel=1
epoch=1
pkgdesc="PDF viewer for research papers and technical books."
arch=(x86_64)
license=(GPL3)
url="https://github.com/ahrm/sioyek"
depends=(qt5-base harfbuzz gumbo-parser openjpeg2 jbig2dec)
makedepends=(git qt5-3d mujs libmupdf)
provides=(sioyek)
conflicts=(sioyek)
source=(
  "git+https://github.com/ahrm/sioyek.git"
  "0001-Mupdf-0.23-support.patch"
)
sha256sums=(
  'SKIP'
  '37c05f9d0da21697827d31b20e91bb38f1f251fb6bf77e08ae86f1150f717f01'
)

pkgver() {
	cd "sioyek"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "sioyek"
  patch -p1 < "../0001-Mupdf-0.23-support.patch"
}

build() {
  cd sioyek
  sed -i 's/\/\/#define LINUX_STANDARD_PATHS/#define LINUX_STANDARD_PATHS/' pdf_viewer/main.cpp
  sed -i 's/-lmupdf-third -lmupdf-threads -lharfbuzz/-lmupdf -lharfbuzz -lfreetype -lgumbo -ljbig2dec -lopenjp2 -ljpeg/' pdf_viewer_build_config.pro
  qmake "CONFIG+=linux_app_image" pdf_viewer_build_config.pro
  make
}

package() {
    cd "sioyek"
    make INSTALL_ROOT="${pkgdir}/" install
    install -D tutorial.pdf -t "${pkgdir}/usr/share/sioyek"
    install -Dm644 -t "${pkgdir}/etc/sioyek" pdf_viewer/keys.config pdf_viewer/prefs.config
    install -Dm644 -t "${pkgdir}/usr/share/man/man1" resources/sioyek.1
    install -d "${pkgdir}/usr/share/sioyek/shaders"
    cp -r pdf_viewer/shaders/* "${pkgdir}/usr/share/sioyek/shaders"
}
