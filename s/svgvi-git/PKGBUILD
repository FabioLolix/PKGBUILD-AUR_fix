# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=svgvi-git
pkgver=2.0.2.r0.ge4655bf
pkgrel=1
pkgdesc="SVG Text Editor and Viewer"
arch=(x86_64)
url="https://gitlab.com/gsvg/svgvi"
license=(GPL-3.0-only)
depends=(gsvgtk gtksourceview5)
makedepends=(git meson vala gresg)
provides=(svgvi)
conflicts=(svgvi)
source=("git+https://gitlab.com/gsvg/svgvi.git")
sha256sums=('SKIP')

pkgver() {
  git -C svgvi describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export CFLAGS+=" -Wno-implicit-function-declaration"
  export CXXFLAGS+=" -Wno-implicit-function-declaration"
  arch-meson svgvi build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
