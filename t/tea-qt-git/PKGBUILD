# Maintainer: Peter Semiletov <peter.semiletov@gmail.com>

pkgname=tea-qt-git
pkgver=60.5.0.r2.ge616bfc
pkgrel=1
pkgdesc="Powerful text editor for Linux, *BSD, Windows, OS/2, Mac and Haiku OS"
arch=(x86_64)
url="http://tea.ourproject.org"
license=(GPL)
depends=(qt6-base qt6-5compat gcc-libs zlib hunspell desktop-file-utils)
makedepends=(cmake git)
provides=(tea-qt)
conflicts=(tea-qt)
source=("git+https://github.com/psemiletov/tea-qt.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/tea-qt"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  mkdir -p "${srcdir}/tea-qt/b"
  cd "${srcdir}/tea-qt/b"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/tea-qt/b"
  make DESTDIR="$pkgdir" install
}
