
pkgname=vegastrike-engine-git
pkgver=0.9.0.r0.g23de3c4b3
pkgrel=1
pkgdesc="A spaceflight simulator in massive universe"
arch=(x86_64)
url="https://www.vega-strike.org/"
license=(GPL-3.0-or-later)
depends=(boost-libs python freeglut gtk3 libvorbis openal sdl2 glu

         # namcap implicit depends
         glibc gcc-libs glib2 zlib libglvnd libpng libjpeg-turbo expat)
makedepends=(git cmake boost)
provides=(vegastrike-engine)
conflicts=(vegastrike-engine)
source=("git+https://github.com/vegastrike/Vega-Strike-Engine-Source#branch=0.9.x")
sha256sums=('SKIP')

pkgver() {
  cd Vega-Strike-Engine-Source
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  # buildtype None, enable ffmpeg, ogre will fail, not supported
  # https://github.com/vegastrike/Vega-Strike-Engine-Source/issues/777#issuecomment-1763235378

  local _flags=(
    -DINSTALL_GTEST=ON  #fail to build using system's gtest
    -DUSE_GTEST=ON
	-DENABLE_PIE=ON
  )

  cmake -B build -S "Vega-Strike-Engine-Source/engine" -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # remove bundled gtest,gmock
  rm -rf ${pkgdir}/usr/include
  rm -rf ${pkgdir}/usr/lib
}
