
pkgname=vegastrike-release-git
pkgver=0.9.1.r0.ge1e31bc5
pkgrel=1
pkgdesc="Vega Strike - Upon the Coldest Sea. Space flight and trading simulator with realistic distances"
arch=(any)
url="https://www.vega-strike.org/"
license=(GPL2)
optdepends=(vegastrike-engine-release-git)
makedepends=(git cmake)
conflicts=(vegastrike)
options=(!strip)
source=("Vega-Strike-Assets-Production::git+https://github.com/vegastrike/Assets-Production#branch=0.9.x")
sha256sums=('SKIP')

pkgver() {
  cd Vega-Strike-Assets-Production
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cmake -B build -S "Vega-Strike-Assets-Production" -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
