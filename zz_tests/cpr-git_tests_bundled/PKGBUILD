# Maintainer: Tobias Backer Dirks <omgitsaheadcrab@gmail.com>
# Contributor: Ivan "Penter" <ivaiva1999ivaiva@gmail.com>

pkgname=cpr-git_tests_bundled
pkgver=1.11.1.r38.g9ae5818
pkgrel=1
pkgdesc="C++ Requests: Curl for People"
arch=(x86_64 i686)
url="https://github.com/libcpr/cpr"
license=(MIT)
depends=(glibc gcc-libs openssl)
makedepends=(git cmake)
provides=(cpr)
conflicts=(cpr)
source=("git+${url}.git") ##branch=1.10.x
sha512sums=("SKIP")

pkgver() {
  cd cpr
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  local _flags=(
    -DCPR_BUILD_TESTS=ON
    -DCPR_USE_SYSTEM_GTEST=OFF
    -DCPR_USE_SYSTEM_CURL=OFF
  )

  cmake -B build -S cpr -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -D cpr/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
