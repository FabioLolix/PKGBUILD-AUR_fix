# Maintainer: John A. Leuenhagen <john@zlima12.com>

pkgname=cpr_tests_bundled
pkgver=1.11.1
pkgrel=1
pkgdesc='C++ Requests: Curl for People, a spiritual port of Python Requests.'
arch=(x86_64 i686)
url="https://github.com/libcpr/cpr"
license=(MIT)
depends=(glibc gcc-libs openssl)
makedepends=(cmake)
provides=(cpr)
conflicts=(cpr)
source=("cpr-${pkgver}.tar.gz::https://github.com/libcpr/cpr/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('e84b8ef348f41072609f53aab05bdaab24bf5916c62d99651dfbeaf282a8e0a2')

build() {
  local _flags=(
    -DCPR_BUILD_TESTS=ON
    -DCPR_USE_SYSTEM_GTEST=OFF
    -DCPR_USE_SYSTEM_CURL=OFF
  )

  cmake -B build -S "cpr-${pkgver}" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -D "cpr-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
