# Maintainer: Yuki Takagi <takagiy.4dev@gmail.com>

pkgname=toml11-git
pkgver=3.7.1.r85.087408a
pkgrel=1
pkgdesc="A C++11 header-only toml parser/encoder depending only on C++ standard library"
arch=(any)
url="https://github.com/ToruNiina/toml11"
license=(MIT)
makedepends=(git cmake)
provides=(toml11)
conflicts=(toml11)
source=("git+https://github.com/ToruNiina/toml11.git")
sha256sums=('SKIP')

pkgver() {
  cd toml11
  git describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "toml11" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_STANDARD=COMPILER \
    -Dtoml11_BUILD_TEST=OFF

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "toml11/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "toml11/README.md" -t "$pkgdir/usr/share/doc/$pkgname/"
}
